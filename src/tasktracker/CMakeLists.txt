set(APP_NAME TaskTracker)

# The MACOSX_BUNDLE_ICON_FILE variable is added to the Info.plist
# generated by CMake. This variable contains the .icns file name,
# without the path.
set(MACOSX_BUNDLE_ICON_FILE app.icns)

# And the following tells CMake where to find and install the file itself.
set(APP_ICON_MACOS "icons/${MACOSX_BUNDLE_ICON_FILE}")
set_source_files_properties(${APP_ICON_MACOS} PROPERTIES MACOSX_PACKAGE_LOCATION "Resources")

qt_add_executable(${APP_NAME}
    main.cpp
    ${APP_ICON_MACOS}
)

qt_add_qml_module(${APP_NAME}
    URI ${APP_NAME}
    VERSION 1.0
    RESOURCE_PREFIX /
    QML_FILES
        qml/AppHeader.qml
        qml/LogsComponent.qml
        qml/config/ConfigDataFolderLocationSelectionPage.qml
        qml/config/ConfigMainPage.qml
        qml/config/ConfigPopup.qml
        qml/main.qml
        qml/task/CalendarPage.qml
        qml/task/DayTask.qml
        qml/task/DayTasks.qml
        qml/task/DayView.qml
        qml/task/DurationEditorPopup.qml
        qml/task/TaskDelegate.qml
        qml/task/TaskEditorPopup.qml
        qml/task/TasksPage.qml
        qml/task/TimeEditor.qml
        qml/task/WeekView.qml
        qml/theme/BottomSeparator.qml
        qml/theme/DockToolBar.qml
        qml/theme/InputDialog.qml
        qml/theme/MaskedImage.qml
        qml/theme/OptionsPopup.qml
        qml/theme/PasswordTextField.qml
        qml/theme/PopupPageHeader.qml
        qml/theme/Theme.js
        qml/theme/ThemedButton.qml
        qml/theme/ThemedButtonDelegate.qml
        qml/theme/ThemedDialog.qml
        qml/theme/ThemedGroupTitle.qml
        qml/theme/ThemedLabel.qml
        qml/theme/ThemedMessageBox.qml
        qml/theme/ThemedPopup.qml
        qml/theme/ThemedRadioDelegate.qml
        qml/theme/ThemedSmallLabel.qml
        qml/theme/ThemedSplitView.qml
        qml/theme/ThemedSwitchDelegate.qml
        qml/theme/ThemedToolButton.qml
        qml/theme/ThemedValueDelegate.qml
        qml/theme/WrappedTextField.qml
        qml/toggl/LoginTogglPopup.qml
        qml/toggl/TogglLoggedStatus.qml
    RESOURCES
        icons/app.svg
        icons/archived.svg
        icons/avatar_mask.svg
        icons/calendar.svg
        icons/clear.svg
        icons/create_new_folder.svg
        icons/password_hidden.svg
        icons/password_visible.svg
        icons/refresh.svg
        icons/settings.svg
        icons/task/decrement.svg
        icons/task/done.svg
        icons/task/edit.svg
        icons/task/increment.svg
        icons/task/start.svg
        icons/task/stop.svg
        icons/task/todo.svg
        icons/tasks.svg
        icons/toggl.svg
        icons/wrap_text.svg
)

set_target_properties(${APP_NAME} PROPERTIES
    MACOSX_BUNDLE_GUI_IDENTIFIER borco.tasktracker
    MACOSX_BUNDLE_BUNDLE_VERSION ${PROJECT_VERSION}
    MACOSX_BUNDLE_SHORT_VERSION_STRING ${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}
    MACOSX_BUNDLE TRUE
    WIN32_EXECUTABLE TRUE
)

target_link_libraries(${APP_NAME} PRIVATE
    QtKeychainAdapter
    QtPlogAdapter
    QtPlogAdapterPlugin
    TaskTrackerLib
    TaskTrackerLibPlugin
)

# install(TARGETS ${APP_NAME}
#     BUNDLE DESTINATION .
#     LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
# )

add_custom_target(${APP_NAME}_mac_deploy_qt
    DEPENDS ${APP_NAME}
    COMMAND
    "${MACDEPLOYQT_EXECUTABLE}"
    "${CMAKE_CURRENT_BINARY_DIR}/${APP_NAME}.app"
    -qmldir="${CMAKE_CURRENT_SOURCE_DIR}/qml"
)

add_custom_target(${APP_NAME}_mac_dmg_installer
    DEPENDS ${APP_NAME}_mac_deploy_qt
    COMMAND
    "${CREATE_DMG_EXECUTABLE}"
    --volname "${APP_NAME} Installer"
    --volicon "${CMAKE_CURRENT_SOURCE_DIR}/${APP_ICON_MACOS}"
    --window-pos 200 120
    --background "${CMAKE_CURRENT_SOURCE_DIR}/macos/background.png"
    --window-size 800 400
    --icon-size 100
    --icon "${APP_NAME}.app" 200 190
    --app-drop-link 600 190
    "${APP_NAME}-Installer.dmg"
    "${CMAKE_CURRENT_BINARY_DIR}/${APP_NAME}.app"
)
